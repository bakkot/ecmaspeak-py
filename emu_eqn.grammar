
# ecmaspeak-py/emu_eqn.grammar:
# A grammar for the content of <emu-eqn> elements.
#
# Copyright (C) 2018  J. Michael Dyck <jmdyck@ibiblio.org>

{START} :
    {EMU_EQN_BODY}{_eos_}

{EMU_EQN_BODY} :
    {CONSTANT_DEF}
    {OPERATION_DEF}
    {MULTI_LINE_EQN}

# --------------------------

{CONSTANT_DEF} :
    {CONSTANT_NAME} = {EXPR}
    {CONSTANT_NAME} = {EXPR} = {EXPR}

# --------------------------

{OPERATION_DEF} :
    {OP_NAME}({var}) = {EXPR}

{MULTI_LINE_EQN} :
    {OP_NAME}({var}){RHSS}{nlai}

{RHSS} :
    {RHSS}{RHS}
    {RHS}

{RHS} :
    {nlai}= {EXPR} if {CONDITION}

{CONDITION} :
    {COND} and {COND}
    {COND}

{COND} :
    {EXPR} &ne; {EXPR}
    {EXPR} &le; {EXPR}
    {EXPR} = {EXPR}
    {EXPR} &le; {EXPR} &lt; {EXPR}

# ------

{EXPR} :
    the largest integer {var} (closest to positive infinity) such that {CONDITION}
    {SUM}

{SUM} :
    {SUM} + {TERM}
    {SUM} - {TERM}
    {TERM}

{TERM} :
    {PRODUCT}

{PRODUCT} :
    {FACTOR} &times; {FACTOR}
    {FACTOR} modulo {FACTOR}
    {FACTOR} / {FACTOR}
    {FACTOR}

{FACTOR} :
    ({SUM})
    {CONSTANT_NAME}
    {PREFIX_PAREN}
    {dec_int_lit}
    {var}

{PREFIX_PAREN} :
    {OPN_BEFORE_PAREN}({EXPR})

{OPN_BEFORE_PAREN} :
    {OP_NAME}
    floor

{CONSTANT_NAME} :
    HoursPerDay
    MinutesPerHour
    SecondsPerMinute
    msPerDay
    msPerHour
    msPerMinute
    msPerSecond

{OP_NAME} :
    DateFromTime
    Day
    DayFromYear
    DayWithinYear
    DaysInYear
    HourFromTime
    InLeapYear
    MinFromTime
    MonthFromTime
    SecFromTime
    TimeFromYear
    TimeWithinDay
    WeekDay
    YearFromTime
    msFromTime

# ------
